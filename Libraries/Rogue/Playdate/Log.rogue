module Playdate

augment Rogue::Global
  METHODS
    method configure_standard_output
      standard_output = Playdate::Log
      standard_error  = Playdate::Log
endAugment

class Log : BufferedPrintWriter [singleton]
  PROPERTIES
    filepath = PROJECT_HOME / "Build/Simulator/Log.txt"

  METHODS
    method init
      if ($target("Simulator"))
        native @|FILE* fp = fopen( $filepath->data->as_utf8, "w" );
                |if (fp)
                |{
                |  fflush( fp );
                |  fclose( fp );
                |}
      endIf

    method flush( buffer:String )
      if (buffer.ends_with('\n')) buffer.remove_last

      native @|Rogue_playdate->system->logToConsole( "%s", $buffer->data->as_utf8 );

      if ($target("Simulator"))
        native @|FILE* fp = fopen( $filepath->data->as_utf8, "a" );
                |if (fp)
                |{
                |  fprintf( fp, "%s\n", $buffer->data->as_utf8 );
                |  fflush( fp );
                |  fclose( fp );
                |}
      endIf

      buffer.clear
endClass

