module Playdate

class Display : Canvas [singleton]
  PROPERTIES
    refresh_rate = 30
    view         : View

  METHODS
    method init
      width  = native("LCD_COLUMNS")->Int32
      height = native("LCD_ROWS")->Int32

    method clear( color:Color )
      native @|Rogue_playdate->graphics->clear( $color.value );

    method clear( pattern:Pattern )
      native @|Rogue_playdate->graphics->clear( (LCDColor)&$pattern );

    method draw_fps( x:Int32, y:Int32 )
      native @|Rogue_playdate->system->drawFPS($x,$y);

    method draw_fps( position:XY )
      native @|Rogue_playdate->system->drawFPS($position.x,$position.y);

    method render
      if (not view)
        println "WARNING: No view has been set up for the display. Create an extended View and be sure to call prior.init if you override init()."
        view = View()  # dummy
      endIf

      clip = null
      temporarily Canvas.current = this
        on_draw
        if (view)
          view.update
          view.draw
        endIf
        after_draw
      endTemporarily

    method set_clip( new_clip:Box? )
      @clip = new_clip
      if (new_clip.exists)
        native @|Rogue_playdate->graphics->setScreenClipRect( $new_clip.value.position.x,
                |  $new_clip.value.position.y, $new_clip.value.size.x, $new_clip.value.size.y );
      else
        native @|Rogue_playdate->graphics->clearClipRect();
      endIf

    method set_refresh_rate( @refresh_rate )
      native @|Rogue_playdate->display->setRefreshRate( $refresh_rate );

endClass
